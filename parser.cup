/*
	Andre
	December 14th 2024
*/

package cup.example;

import classes.*;

import java_cup.runtime.*;
import cup.example.Lexer;
import java.io.IOException;
import java.util.ArrayList;
import java.io.File;
import java.io.FileInputStream;

parser code {:
  	protected Lexer lexer;
 	protected Tree syntaxTree = new Tree();
	
 	public Tree getSyntaxTree()
  	{
		return syntaxTree;
  	}

	protected TreeNode createTypeSpecifier(String typeName) { throw new UnsupportedOperationException(); }
	protected TreeNode createFunctionDeclarationNode(TreeNode ts, String i, TreeNode ps, TreeNode cs) { throw new UnsupportedOperationException(); }
 	protected TreeNode createVarDeclaration(TreeNode ts, String i, Integer n) { throw new UnsupportedOperationException(); }
 	protected TreeNode createDeclList(TreeNode dl, TreeNode d) { throw new UnsupportedOperationException(); }
	protected TreeNode createParamList(TreeNode pl, TreeNode p) { throw new UnsupportedOperationException(); }
	protected TreeNode createParam(TreeNode ts, String i, Integer array) { throw new UnsupportedOperationException(); }
	protected TreeNode createStatementList(TreeNode sl, TreeNode s) { throw new UnsupportedOperationException(); }
	protected TreeNode createLocalDecl(TreeNode ld, TreeNode vd) { throw new UnsupportedOperationException(); }
	protected TreeNode selectionStmt(TreeNode exp, TreeNode stmt, TreeNode else_stmt) { throw new UnsupportedOperationException(); }
	protected TreeNode iterationStmt(TreeNode exp, TreeNode stmt) { throw new UnsupportedOperationException(); }
	protected TreeNode compoundStmt(TreeNode ld, TreeNode sl) { throw new UnsupportedOperationException(); }
	protected TreeNode expressionStmt(TreeNode exp) { throw new UnsupportedOperationException(); }
	protected TreeNode returnStatement(TreeNode exp) { throw new UnsupportedOperationException(); }
	protected TreeNode addExpression(TreeNode exp, TreeNode v) { throw new UnsupportedOperationException(); }
	protected TreeNode addVar(String i, TreeNode exp) { throw new UnsupportedOperationException(); }
	protected TreeNode simpleExpression(TreeNode ae, TreeNode rel, TreeNode exp) { throw new UnsupportedOperationException(); }
	protected TreeNode additiveExpression(TreeNode ae, TreeNode op, TreeNode t) { throw new UnsupportedOperationException(); }
	protected TreeNode addTerm(TreeNode tm, TreeNode op, TreeNode f) { throw new UnsupportedOperationException(); }
	protected TreeNode argList(TreeNode al, TreeNode exp) { throw new UnsupportedOperationException(); }

:}

/* define how to connect to the scanner! */
init with {:
  ComplexSymbolFactory f = new ComplexSymbolFactory();
  symbolFactory = f;
  File file = new File("input.txt");
  FileInputStream fis = null;
  try {
    fis = new FileInputStream(file);
  } catch (IOException e) {
    e.printStackTrace();
  } 
  lexer = new Lexer(f,fis);
:};
scan with {: return lexer.next_token(); :};

/* Terminals (tokens returned by the scanner). */
terminal            SEMI, PLUS, MINUS, TIMES, LPAREN, RPAREN, DIVIDE;
terminal            LE, LT, GE, GT, EQ, NEQ, ASSIGN, COMMA, LBRACK, RBRACK, LBRACE, RBRACE;
terminal            ELSE, IF, INT, RETURN, VOID, WHILE, CEND, CSTART;
terminal Integer    NUM;
terminal String     ID;

/* Non terminals */
non terminal   TreeNode  program;
non terminal   TreeNode  declaration_list, declaration, var_declaration, type_specifier;
non terminal   TreeNode  fun_declaration, params, param_list, param, compound_stmt;
non terminal   TreeNode  local_declarations, statement_list, statement;
non terminal   TreeNode	 expression_stmt, selection_stmt, iteration_stmt, return_stmt;
non terminal   TreeNode  expression, var, simple_expression, relop;
non terminal   TreeNode	 additive_expression, addop, term, mulop;
non terminal   TreeNode	 factor, call, args, arg_list;

/* Precedences */
precedence nonassoc ELSE;	// Inserted by Eclipse plugin
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LT, LE, GT, GE;
precedence left EQ, NEQ;
precedence left ASSIGN;

program ::= declaration_list:dl													{: 
																				   	RESULT = syntaxTree.create("Program", dl);
																				:};

declaration_list ::= declaration_list:dl declaration:d 							{: 
																				   	RESULT = createDeclList(dl, d);
 																				:}
                   | declaration:d 												{: 
																					RESULT = createDeclList(null, d);
 																				:};

declaration ::= var_declaration:vd 												{:  
																				   	RESULT = vd;
																				:}
              | fun_declaration:fd 												{: 
																				   	RESULT = fd;
    																			:};

var_declaration ::= type_specifier:ts ID:i SEMI 								{: 
																					RESULT = createVarDeclaration(ts, i, 0);
																				:}
                  | type_specifier:ts ID:i LBRACK NUM:n RBRACK SEMI 			{: 
																					RESULT = createVarDeclaration(ts, i, n);
   																				:};

type_specifier ::= INT 															{: 
																				   	RESULT = createTypeSpecifier("INT");
																				:}
                 | VOID 														{:  
																					RESULT = createTypeSpecifier("VOID");
																				:};
fun_declaration ::= type_specifier:ts ID:i LPAREN params:ps RPAREN compound_stmt:cs {: 
																					RESULT = createFunctionDeclarationNode(ts, i, ps, cs);
    																			 :};

params ::= param_list:pl														{: 
																				   	RESULT = pl;
    																			:}
         | VOID 																{: 
																				   	RESULT = new TreeNode("params - VOID");
    																			:};

param_list ::= param_list:pl COMMA param:p 										{: 
																					RESULT = createParamList(pl, p);
    																			:}
             | param:p 															{: 
																					RESULT = createParamList(null, p);
    																			:};

param ::= type_specifier:ts ID:i 												{: 
																					RESULT = createParam(ts, i, 0);
    																			:}
        | type_specifier:ts ID:i LBRACK RBRACK 									{: 
																					RESULT = createParam(ts, i, 1);
    																		    :};

compound_stmt ::= LBRACE local_declarations:ld statement_list:sl RBRACE 		{: 
        																		   	RESULT = compoundStmt(ld, sl);
    																			:};

local_declarations ::= local_declarations:ld var_declaration:vd 				{: 
																					RESULT = createLocalDecl(ld, vd);
    																			:}
                     | /* ε */ 													{: 
																					RESULT = createLocalDecl(null, null); //new TreeNode("local_declarations");
    																			:};

statement_list ::= statement_list:sl statement:s								{: 
																					RESULT = createStatementList(sl, s);
    																			:}
                 | /* ε */ 														{:
																				   	RESULT = createStatementList(null, null); //new TreeNode("statement_list");
    																			:};

statement ::= expression_stmt:es 												{: 
    																			   RESULT = es;
																				:}
            | compound_stmt:cs 													{:
    																			   RESULT = cs;
																				:}
            | selection_stmt:ss 												{: 
    																			   RESULT = ss;
																				:}
            | iteration_stmt:is 												{: 
    																			   RESULT = is;
																				:}
            | return_stmt:rs 													{: 
    																			   RESULT = rs;
																				:};

expression_stmt ::= expression:exp SEMI 										{: 
    																			   RESULT = expressionStmt(exp);
																				:}
                  | SEMI 														{: 
    																			   RESULT = expressionStmt(null);
																				:};

selection_stmt ::= IF LPAREN expression:exp RPAREN statement:stmt 				{: 
    																			   RESULT = selectionStmt(exp, stmt, null);
																				:}
                 | IF LPAREN expression:exp RPAREN statement:stmt ELSE statement:else_stmt 		{: 
    																			   RESULT = selectionStmt(exp, stmt, else_stmt);
																				:};

iteration_stmt ::= WHILE LPAREN expression:exp RPAREN statement:stmt 			{: 
   																				   RESULT = iterationStmt(exp, stmt);
																			    :};

return_stmt ::= RETURN SEMI 													{: 
																					RESULT = returnStatement(null);
																				:}
              | RETURN expression:exp SEMI 										{: 
																					RESULT = returnStatement(exp);
																				:};

expression ::= var:v ASSIGN expression:exp 										{: 
																				   RESULT = addExpression(exp, v);
																				:}
             | simple_expression:exp 											{: 
																				   RESULT = addExpression(exp, null);
																				:};


var ::= ID:i 																	{: 
    																			   RESULT = addVar(i, null);
																				:}
      | ID:i LBRACK expression:exp RBRACK 										{: 
    																			   RESULT = addVar(i, exp);
																				:};

simple_expression ::= additive_expression:ae relop:rel additive_expression:exp	{: 
    																			   RESULT = simpleExpression(ae, rel, exp);
																				:}
                    | additive_expression:ae 									{: 
    																			   RESULT = simpleExpression(ae, null, null);
																				:};

relop ::= LE 																	{: 
																				   RESULT = new TreeNode("LE");
																				:}
        | LT 																	{: 
																				   RESULT = new TreeNode("LT");
																				:}
        | GT 																	{:
																				   RESULT = new TreeNode("GT");
																				:}
        | GE 																	{:
																				   RESULT = new TreeNode("GE");
																				:}
        | EQ 																	{: 
																				   RESULT = new TreeNode("EQ");
																				:}
        | NEQ 																	{: 
																				   RESULT = new TreeNode("NEQ");
																				:};

additive_expression ::= additive_expression:ae addop:op term:t 					{: 
    																			   RESULT = additiveExpression(ae, op, t);
																				:}
                      | term:t 													{: 
    																			   RESULT = additiveExpression(null, null, t);
																				:};

addop ::= PLUS 																	{: 
																				   RESULT = new TreeNode("PLUS");
																				:}
        | MINUS 																{: 
																				   RESULT = new TreeNode("MINUS");
																				:};

term ::= term:tm mulop:op factor:f 												{: 
    																			   RESULT = additiveExpression(tm, op, f);
																				:}
       | factor:f 																{: 
    																			   RESULT = additiveExpression(null, null, f);
																				:};

mulop ::= TIMES 																{: 
																				   RESULT = new TreeNode("TIMES");
																				:}
        | DIVIDE 																{: 
  																				   RESULT = new TreeNode("DIVIDE");
																				:};

factor ::= LPAREN expression:exp RPAREN 										{: 
																   				   TreeNode node = new TreeNode("factor");
    																			   node.addChild(new TreeNode("LPAREN"));
    																			   node.addChild(exp); 
   																				   node.addChild(new TreeNode("RPAREN"));
    																			   RESULT = node;
																				:}
         | var:v 																{: 
																				   RESULT = v;
																				:}
         | call:c 																{: 
         																		   RESULT = c;
																				:}
         | NUM:num 																{: 
																				   TreeNode node = new TreeNode("NUM");
    																			   node.setData(num.toString());
    																			   RESULT = node;
																				:};

call ::= ID:i LPAREN args:arg RPAREN 											{: 
																				   TreeNode node = new TreeNode("call");
    																			   node.addChild(new TreeNode(i.toString()));
    																			   node.addChild(arg); 
    																			   RESULT = node;
																				:};

args ::= arg_list:al 															{: 
																				   RESULT = al; 
																				:}
       | /* ε */ 																{:
 																				   RESULT = new TreeNode("empty_args");
																				:};


arg_list ::= arg_list:al COMMA expression:exp 									{: 
    																			   RESULT = argList(al, exp);
																				:}
           | expression:exp 													{: 
    																			   RESULT = argList(null, exp);
																				:};

       